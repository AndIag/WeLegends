apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion versions.compiledSdk
    buildToolsVersion "$versions.buildTools"
    defaultConfig {
        applicationId "com.andiag.welegends"
        minSdkVersion 19
        targetSdkVersion versions.compiledSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HttpLoggingInterceptorLevel", "\"${getRetrofitLoggingLevel()}\""
            buildConfigField "String", "AndIagApi", "\"${getAndIagApi()}\""
            buildConfigField "String", "ApiKey", "\"${getApiKey()}\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HttpLoggingInterceptorLevel", "NONE"
            buildConfigField "String", "AndIagApi", "\"${getAndIagApi()}\""
            buildConfigField "String", "ApiKey", "\"${getApiKey()}\""
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            // Force all of the primary support libraries to use the same version.
            if (details.requested.group == 'com.android.support') {
                details.useVersion versions.supportLib
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    kapt "com.jakewharton:butterknife-compiler:$versions.butterknife"
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:$versions.dbflow"

    androidTestCompile "junit:junit:4.12"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "org.mockito:mockito-core:$versions.mockito"

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile "com.android.support:design:$versions.supportLib"
    compile "com.android.support:cardview-v7:$versions.supportLib"
    compile "com.android.support:preference-v14:$versions.supportLib"

    compile 'com.github.siyamed:android-shape-imageview:0.9.3@aar'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.8.0'
    compile 'com.github.florent37:diagonallayout:1.0.3'
    compile('com.hariofspades.gradientartist:gradientartist:0.0.1', {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    })
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile "com.jakewharton:butterknife:$versions.butterknife"

    // Async library for Kotlin
    compile 'org.jetbrains.anko:anko-sdk19:0.9.1'

    //Network access
    compile "com.squareup.retrofit2:converter-gson:$versions.retrofit"
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    //Database
    compile "com.github.Raizlabs.DBFlow:dbflow:$versions.dbflow"
    compile "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:$versions.dbflow@aar"

    compile 'com.github.iagocanalejas.andiag-mvp:commons:0.3'
    compile 'com.github.andy135:StatedLayout:1.0.0'
}

kapt {
    generateStubs = true
}
repositories {
    mavenCentral()
}

def getRetrofitLoggingLevel() {
    Properties properties = new Properties()
    properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
    return properties.getProperty('HttpLoggingInterceptorLevel', "BODY")
}

def getAndIagApi() {
    Properties properties = new Properties()
    properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
    return properties.getProperty('AndIagApi', "http://andiag-prod.apigee.net/v1/test-welegends/")
}

def getApiKey() {
    Properties properties = new Properties()
    properties.load(new File(rootDir.absolutePath + "/local.properties").newDataInputStream())
    return properties.getProperty('ApiKey', "")
}